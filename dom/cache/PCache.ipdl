/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this file,
 * You can obtain one at http://mozilla.org/MPL/2.0/. */

include protocol PBackground;
include protocol PCacheStreamControl;
include PCacheTypes;
include protocol PFileDescriptorSet;

include protocol PBlob; // FIXME: bug 792908

using mozilla::dom::cache::RequestId from "mozilla/dom/cache/IPCUtils.h";

namespace mozilla {
namespace dom {
namespace cache {

protocol PCache
{
  manager PBackground;

parent:
  Match(RequestId requestId, PCacheRequest request, PCacheQueryParams params);
  MatchAll(RequestId requestId, PCacheRequestOrVoid request, PCacheQueryParams params);
  Add(RequestId requestId, PCacheRequest request);
  AddAll(RequestId requestId, PCacheRequest[] requests);
  Put(RequestId requestId, PCacheRequest request, PCacheResponse response);
  Delete(RequestId requestId, PCacheRequest request, PCacheQueryParams params);
  Keys(RequestId requestId, PCacheRequestOrVoid request, PCacheQueryParams params);

child:
  MatchResponse(RequestId requestId, nsresult aRv, PCacheResponseOrVoid aResponse,
                nullable PCacheStreamControl aStreamControl);
  MatchAllResponse(RequestId requestId, nsresult aRv, PCacheResponse[] responses,
                   nullable PCacheStreamControl aStreamControl);
  AddResponse(RequestId requestId, nsresult aRv);
  AddAllResponse(RequestId requestId, nsresult aRv);
  PutResponse(RequestId requestId, nsresult aRv);
  DeleteResponse(RequestId requestId, nsresult aRv, bool success);
  KeysResponse(RequestId requestId, nsresult aRv, PCacheRequest[] requests,
               nullable PCacheStreamControl aStreamControl);

both:
  __delete__();
};

} // namespace cache
} // namespace dom
} // namespace mozilla
